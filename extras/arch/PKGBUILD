pkgname=snail-lang
pkgver=0.7.6
pkgrel=1
pkgdesc="Snail programming language interpreter"
arch=('x86_64')
url="https://github.com/sudonym1/snail"
license=('MIT')
depends=('python>=3.8' 'python-jmespath')
makedepends=('cargo' 'python-build' 'python-installer' 'python-maturin' 'rust')
source=("$pkgname-$pkgver.tar.gz::https://github.com/sudonym1/snail/archive/refs/tags/v$pkgver.tar.gz")
sha256sums=('SKIP')

build() {
  export GIT_CEILING_DIRECTORIES="$srcdir"
  cd "$srcdir/snail-$pkgver"
  python -m build --wheel --no-isolation
}

package() {
  cd "$srcdir/snail-$pkgver"
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
